#!/usr/bin/env perl

my $VERSION = '0.04';

use strict;
use warnings FATAL => 'all';

use NDTools::INC;

use Getopt::Long qw(:config bundling);
use Log::Log4Cli;
use Pod::Usage;
use Struct::Path qw(slist spath);
use Struct::Path::PerlStyle qw(ps_parse ps_serialize);

use NDTools::Slurp qw(st_dump st_load);
use NDTools::Struct qw(st_copy st_merge);

# defaults
my $OPTS = {
    'path' => '', # empty means whole doc
    'verbose' => 0,
};

my @files;

GetOptions (
    'depth|d=i' => \$OPTS->{'depth'},
    'help|h' => sub {
        pod2usage(-exitval => 1, -output => \*STDERR,
        -sections => 'SYNOPSIS|OPTIONS|EXAMPLES', -verbose => 99)
    },
    'list|l' => \$OPTS->{'list'},
    'out-fmt=s' => \$OPTS->{'out-fmt'},
    'path|p=s' => \$OPTS->{'path'},
    'verbose|v:+' => \$OPTS->{'verbose'},
    'version|ver' => sub { print "$VERSION\n"; exit 0; },
) || pod2usage(-exitval => 1, -output  => \*STDERR);

$Log::Log4Cli::LEVEL = $OPTS->{'verbose'};

while (my $f = shift @ARGV) { push @files, { src => $f }};

die_fatal "No one file specified", 1 unless (@files);

for my $file (@files) {
    log_debug { "Processing file '$file->{src}'" };
    my $data = st_load($file->{src}, undef);
    my $path = ps_parse($OPTS->{path});
    my ($out) = spath($data, $path, deref => 1);
    if ($OPTS->{list}) {
        for my $i (slist($out, depth => $OPTS->{'depth'})) {
            print ps_serialize($i->[0]) . "\n";
        }
    } else {
        st_dump(\*STDOUT, $out, $OPTS->{'out-fmt'});
    }
}

die_info "All done", 0;

__END__

=head1 NAME

ndquery - Get desired parts from nested data structure

=head1 SYNOPSIS

    ndquery [OPTIONS]
    ndquery [OPTIONS] <arguments>

=head1 DESCRIPTION

Get specified by path parts from nested data structure

=head1 OPTIONS

=over 4

=item B<--depth|-d> E<lt>intE<gt>

Combined with B<--list> allows to define how much levels must be listed

=item B<--help|-h>

Print a help message and exit.

=item B<--list|-l>

List structure's paths

=item B<--out-fmt> E<lt>JSON|YAMLE<gt>

Output format selector. JSON used by default.

=item B<--path|-p> E<lt>stringE<gt>

Path in the structure to deal with.

=item B<--verbose|-v> [int]

Increase verbosity, max level - 4.

=item B<--version|--ver>

Print version and exit.

=back

=head1 EXAMPLES

Show as pretty printed canonical JSON:

    ndquery struct.json

Get specified path from document:

    ndquery --path '{some}{path}[2]' struct.json

List subpaths:

    ndquery --list --path '{some}{path}[2]' struct.json
