#!/usr/bin/env perl

my $VERSION = '0.07';

use strict;
use warnings FATAL => 'all';

use NDTools::INC;

use Getopt::Long qw(:config bundling);
use Log::Log4Cli;
use Pod::Usage;
use Struct::Path qw(slist spath spath_delta);
use Struct::Path::PerlStyle qw(ps_parse ps_serialize);
use Term::ANSIColor qw(colored);

use NDTools::Slurp qw(s_dump st_load);

# defaults
my $OPTS = {
    'color-common' => 'bold black',
    'path' => '', # empty means whole doc
};

GetOptions (
    'depth|d=i' => \$OPTS->{depth},
    'help|h' => sub {
        pod2usage(-exitval => 1, -output => \*STDERR,
        -sections => 'SYNOPSIS|OPTIONS|EXAMPLES', -verbose => 99)
    },
    'inject=s' => \$OPTS->{inject},
    'list|l' => \$OPTS->{list},
    'path|p=s' => \$OPTS->{path},
    'out-fmt=s' => \$OPTS->{'out-fmt'},
    'verbose|v:+' => \$OPTS->{verbose},
    'version|ver' => sub { print "$VERSION\n"; exit 0; },
) || pod2usage(-exitval => 1, -output  => \*STDERR);

$Log::Log4Cli::LEVEL = $OPTS->{verbose} || 0;
$OPTS->{colors} = -t STDOUT ? 1 : 0 unless (defined $OPTS->{colors});

my $path = eval_fatal { ps_parse($OPTS->{path}) } 2, "Failed to parse path";

for my $file (@ARGV ? @ARGV : \*STDIN) {
    log_debug { "Processing " . (ref $file ? "STDIN" : "file '$file'") };
    my $data = st_load($file, undef);

    my @out; # orig structure may be splitted to many
    if ($OPTS->{inject}) {
        my $val = st_load($OPTS->{inject}, undef);
        if (@{$path}) {
            map { ${$_} = $val } spath($data, $path, expand => 1);
            push @out, $data;
        } else {
            push @out, $val;
        }
    } else {
        @out = spath($data, $path, deref => 1);
    }

    if ($OPTS->{list}) {
        for my $data (@out) {
            my $prev;
            for my $i (slist($data, depth => $OPTS->{depth})) {
                my @delta = spath_delta($prev, $i->[0]);
                my $cmn = @{$i->[0]} - @delta;
                $cmn = ($cmn and @{$i->[0]} != @delta) ? [ @{$i->[0]}[0 .. $cmn - 1] ] : [];
                $cmn = $OPTS->{colors} ?
                    colored(ps_serialize($cmn), $OPTS->{'color-common'}) : ps_serialize($cmn);
                print $cmn . ps_serialize(\@delta) . "\n";
                $prev = $i->[0];
            }
        }
    } else {
        s_dump(\*STDOUT, $OPTS->{'out-fmt'}, undef, @out);
    }
}

die_info "All done", 0;

__END__

=head1 NAME

ndquery - Get desired parts from nested data structure

=head1 SYNOPSIS

    ndquery [OPTIONS]
    ndquery [OPTIONS] <arguments>

=head1 DESCRIPTION

Get specified by path parts from nested data structure

=head1 OPTIONS

=over 4

=item B<--depth|-d> E<lt>intE<gt>

Combined with B<--list> allows to define how much levels must be listed

=item B<--help|-h>

Print a help message and exit.

=item B<--inject> E<lt>fileE<gt>

Load data from file and inject to structire using path as a pointer.

=item B<--list|-l>

List structure's paths

=item B<--out-fmt> E<lt>JSON|YAMLE<gt>

Output format selector. JSON used by default.

=item B<--path|-p> E<lt>stringE<gt>

Path in the structure to deal with.

=item B<--verbose|-v> [int]

Increase verbosity, max level - 4.

=item B<--version|--ver>

Print version and exit.

=back

=head1 EXAMPLES

Show as pretty printed canonical JSON:

    ndquery struct.json

Get specified path from document:

    ndquery --path '{some}{path}[2]' struct.json

List subpaths:

    ndquery --list --path '{some}{path}[2]' struct.json
