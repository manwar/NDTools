#!/usr/bin/env perl

use strict;
use warnings FATAL => 'all';

use NDTools::INC;

use File::Basename qw(basename);
use Getopt::Long qw(:config bundling pass_through);
use Log::Log4Cli;
use Module::Find;

my $OPTS;
use constant MODDIR => "NDTools::NDDiff";

GetOptions (
    'help|h' => \$OPTS->{'help'},
    'module|m=s' => \$OPTS->{'module'},
    'list-modules|l' => \$OPTS->{'list-modules'},
    'verbose|v:+' => \$OPTS->{'verbose'},
    'version|V' => \$OPTS->{'version'},
) || pod2usage(-exitval => 1, -output  => \*STDERR);

$Log::Log4Cli::LEVEL = $OPTS->{'verbose'} || 0;

$OPTS->{'module'} = basename($0) unless (defined $OPTS->{'module'});
my $MODS = { map { $_, 1 } usesub MODDIR };

if ($OPTS->{'list-modules'}) {
    for my $m (sort keys %{$MODS}) {
        printf "%-10s %-8s %s\n", (split('::', $m))[-1], $m->VERSION, $m->MODINFO;
    }
    die_info "All done", 0;
}

my $mod = MODDIR . "::" . $OPTS->{'module'};
die_fatal "Failed to locate module '$OPTS->{'module'}' in " . MODDIR, 1
    unless exists ($MODS->{$mod});
eval "use $mod";
die_fatal "Unable to load module '$OPTS->{'module'}", 1 if ($@);

my $dm = $mod->new() or die_fatal undef, 1;

if ($OPTS->{'help'}) {
    $dm->usage;
    exit 1;
} elsif ($OPTS->{'version'}) {
    print $dm->VERSION . "\n";
} else {
    $dm->run or die_fatal undef, 1;
    die_info undef, $dm->status;
}

die_info "All done", 0;
